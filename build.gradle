// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
        flatDir {
            dirs 'libs','../libs'
        }
    }
}

/**
 * Read build properties from signing.properties file on root directory
 * @return props - array of read properties
 */

def readCustomBuildProperties() {
    def Properties props = new Properties()
    println("Load custom build properties")
    def propFile = new File("signing.properties")
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))
    } else {
        println("signing.properties not found")
    }
    return props
}


/**
 * Overriride default signing config with definitions from signing.properties file
 *  signingConfig - build signing configuration
 */

def overrideSigningConfig(signingConfig) {
    def Properties props = readCustomBuildProperties()
    if (props != null
            && props.containsKey("KEYSTORE_FILE")
            && props.containsKey("KEYSTORE_PASSWORD")
            && props.containsKey("KEY_ALIAS")
            && props.containsKey("KEY_PASSWORD")) {

        def keystoreFile = new File(props["KEYSTORE_FILE"])
        if (keystoreFile.canRead()) {
            println("Load Signing config.")
            signingConfig.storeFile = keystoreFile
            signingConfig.storePassword = props["KEYSTORE_PASSWORD"]
            signingConfig.keyAlias = props["KEY_ALIAS"]
            signingConfig.keyPassword = props["KEY_PASSWORD"]
        } else {
            println("keystore file not found: " + props["KEYSTORE_FILE"])
        }
    } else {
        println("Error!!! signing.properties found but some entries missing")
    }
}
