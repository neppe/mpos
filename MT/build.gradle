apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def appVersionName = "3.0."
def appVersionCode = System.getenv("BUILD_NUMBER") as Integer ?: 0

def props = new Properties()
file("release.properties").withInputStream { props.load(it) }

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.3"

    signingConfigs {
        release
    }

    defaultConfig {
        applicationId "com.verifone.swordfish.manualtransaction"
        minSdkVersion 22
        targetSdkVersion 26

        versionCode appVersionCode
        versionName appVersionName + appVersionCode

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        project.ext.set("archivesBaseName", "MT-" + versionName + "-" + versionCode)

        //inject api key from props file to manifest
        manifestPlaceholders = [fabric_api_key: props.FABRIC_API_KEY]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            overrideSigningConfig(signingConfig)
            zipAlignEnabled true
        }
        debug {
            minifyEnabled false
            zipAlignEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile(name: 'DeveloperSDK', ext: 'aar')
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.1.0-beta4'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:design:26.1.0'
    testCompile 'junit:junit:4.12'

    compile 'pl.mg6.hrisey:hrisey:0.3.1'
    compile 'com.google.code.gson:gson:2.8.2'

    compile('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }
}

/**
 * Override default signing config with definitions from release.properties file
 *  signingConfig - build signing configuration
 */
def overrideSigningConfig(signingConfig) {
    def props = new Properties()
    println("Load MT build properties")
    props.load(new FileInputStream(project.file("release.properties")))
    signingConfig.storeFile project.file(props.KEYSTORE_FILE)
    signingConfig.storePassword props.KEYSTORE_PASSWORD
    signingConfig.keyAlias props.KEY_ALIAS
    signingConfig.keyPassword props.KEY_ALIAS_PASSWORD
}
